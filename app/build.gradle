import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'realm-android'

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("dd MMMMM yyyy");
    return df.format(new Date()) + " Ð³.";
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    def fileNaming = "apk/ForPDA"

    def versionNumber = 76

    def versionMajor = "0"
    def versionMinor = "6"
    def versionPatch = "8.5"

    def versionBuild = -1
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['DATE_BUILD'] = getDateTime()
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        applicationId "ru.forpdateam.forpda"
        versionCode versionNumber
        versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
        minSdkVersion 19
        targetSdkVersion 27
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", 'BUILD_DATE', '"' + getDateTime() + '"'
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    flavorDimensions "wtf"
    productFlavors {
        stable {
            dimension "wtf"
        }
        beta {
            applicationId 'ru.forpdateam.forpda.beta'
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild}) beta"
            dimension "wtf"
        }

        dev {
            minSdkVersion '21'
            applicationId 'ru.forpdateam.forpda.debug'
            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild}) dev_api_21"
            dimension "wtf"
        }
    }

    /*applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileNaming = "apk/ForPDA"
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(getProject().getRootDir(), "${fileNaming}-${versionMajor}.${versionMinor}.${versionPatch}-${outputFile.name}")
            }
        }
    }*/

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${fileNaming}-${versionMajor}.${versionMinor}.${versionPatch}.apk"
        }
    }


    dexOptions {
        dexInProcess = true
        preDexLibraries = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
    }
    splits {
        abi {
            enable gradle.startParameter.taskNames.contains(":app:assembleRelease")
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }
    lintOptions {
        disable 'RestrictedApi'
        checkReleaseBuilds false
    }
}

ext {
    supportLibVersion = '27.1.0'
    okHttpVersion = '3.9.0'
    rxCoreVersion = '2.1.5'
    rxAndroidVersion = '2.0.1'
    archVersion = '1.0.0-alpha5'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:palette-v7:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:preference-v7:${supportLibVersion}"
//    implementation "android.arch.persistence.room:runtime:${archVersion}"
//    implementation "android.arch.persistence.room:rxjava2:${archVersion}"
//    annotationProcessor "android.arch.persistence.room:compiler:${archVersion}"
//    implementation "android.arch.lifecycle:runtime:${archVersion}"
//    implementation "android.arch.lifecycle:extensions:${archVersion}"
//    annotationProcessor "android.arch.lifecycle:compiler:${archVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okHttpVersion}"
    implementation 'com.annimon:stream:1.1.4'
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxCoreVersion}"
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation group: 'org.ccil.cowan.tagsoup', name: 'tagsoup', version: '1.2'
    implementation 'org.bsc.util:minitemplator-repackaged:1.2'
    implementation 'com.lapism:searchview:4.0'
    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.afollestad:sectioned-recyclerview:0.5.0'
    implementation 'com.faendir:acra:4.10.0'

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.wada811:android-material-design-colors:3.0.0'
    implementation 'com.thebluealliance:spectrum:0.7.1'
    implementation 'com.github.douglasjunior:android-simple-tooltip:0.2.0'
    implementation 'com.robohorse.pagerbullet:pagerbullet:1.0.8'
    implementation 'com.github.apg-mobile:android-round-textview:v0.0.3'
    implementation 'com.evernote:android-job:1.2.0'
}
